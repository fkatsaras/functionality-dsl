Server SimpleJSON
  host: "localhost"
  port: 8080
  cors: "http://localhost:3000"
end

// --- External REST sources -------------------------------------
ExternalREST RealObjects
  url: "https://api.restful-api.dev/objects"
  verb: GET
end

ExternalREST RealCreate
  url: "https://api.restful-api.dev/objects"  
  verb: POST
end

ExternalREST RealUpdate
  url: "https://api.restful-api.dev/objects"
  verb: PUT
end

ExternalREST RealDelete
  url: "https://api.restful-api.dev/objects"
  verb: DELETE
end

// --- Entity ----------------------------------------------------
// Upstream JSON looks like:
// {
//   "id": "...",
//   "name": "...",
//   "data": {
//      "color": "...",
//      "capacity": "...",
//      "price": ...,
//      "year": ...
//   }
// }

Entity RealObjectsRaw
  source: RealObjects
  attributes:
    - raw: list
end

Entity RealObjectsRows(RealObjectsRaw)
  attributes:
    - lol: list = [{ 
      "id": x['id'],
      "name": x['name'],
      "color": x['data']['color'],
      "capacity": x['data']['capacity'],
      "price": x['data']['price'],
      "year": x['data']['year']
      } for x in RealObjectsRaw.raw];
end

// --- Internal endpoint -----------------------------------------
InternalREST RealObjectsList
  entity: RealObjectsRows
  path: "/api/objects"
end

// --- Components ------------------------------------------------
Component<Table> ObjectsTable
  endpoint: RealObjectsList
  colNames: ["id", "name", "color", "capacity", "price", "year"]
end

Component<ActionForm> NewObject
  action: RealCreate
  fields: [name, data]
  submitLabel: "Create Object"
end

Component<ActionForm> EditObject
  action: RealUpdate
  fields: [id, name, data]
  pathKey: id
  submitLabel: "Update Object"
end

Component<ActionForm> DeleteObject
  action: RealDelete
  fields: [id]
  pathKey: id
  submitLabel: "Delete Object"
end
