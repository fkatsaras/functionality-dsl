// ============================================
// FLOW: Cart Updates (Real-time WebSocket)
// ============================================
// Private WebSocket endpoint for real-time cart update notifications

// External source - Cart WebSocket service
Source<WS> CartUpdatesAPI
  channel: "ws://host.docker.internal:9003"
  subscribe:
    type: object
    entity: CartUpdateMessage
  publish:
    type: string
    entity: SessionId
end

// WebSocket cart update message structure
Entity CartUpdateMessage
  attributes:
    - type: string;
    - sessionId: string;
    - cart: object;
    - itemCount: integer;
    - total: number;
    - timestamp: string;
end

// Internal endpoint - subscribes to the source with session id, streams cart updates to clients
Endpoint<WS> CartUpdates
  channel: "/ws/cart"
  publish:
    type: string
    entity: SessionId
  subscribe:
    type: object
    entity: CartUpdateView
end

// Client input - just the session ID as a string
Entity SessionId
  attributes:
    - value: string;
end

// Transform: Extract cart data with items array
Entity CartUpdateView(CartUpdateMessage)
  attributes:
    - sessionId: string = CartUpdateMessage["sessionId"];
    - items: array = get(CartUpdateMessage["cart"], "items", []);
    - itemCount: integer = CartUpdateMessage["itemCount"];
    - total: number = CartUpdateMessage["total"];
    - timestamp: string = CartUpdateMessage["timestamp"];
end

// UI Component: LiveTable showing cart items with real-time updates
Component<LiveTable> LiveCartTable
  endpoint: CartUpdates
  arrayField: "items"
  keyField: "productId"
  columns:
    - "productId": string
    - "productName": string
    - "quantity": integer
    - "price": number
  label: "Shopping Cart"
  maxRows: 50
end

Component<Input> CartSelect
  endpoint: CartUpdates
  placeholder: "Enter session ID..."
  label: "SessionID"
  submitLabel: "Show"
end