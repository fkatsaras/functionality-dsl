Server AuthServer
  host: "localhost"
  port: 8081
  cors: "http://localhost:5173"
  loglevel: debug
end

// ============================================================
// SCHEMAS (Pure Data Structures)
// ============================================================

// Common reusable schemas
Entity StandardMessageResponse
  attributes:
    - id: integer;
    - message: string;
end

Entity SuccessResponse
  attributes:
    - success: boolean;
    - message: string;
end

Entity UsersListSchema
  attributes:
    - users: array;
end

// Request schemas (input validation)
Entity UserRegisterRequest
  attributes:
    - username: string(3..50);
    - password: string(6..);
    - email: string<email>;
end

Entity LoginRequest
  attributes:
    - username: string;
    - password: string;
end

Entity ResetPasswordRequest
  attributes:
    - email: string<email>;
    - newPassword: string(6..);
end

Entity UserUpdateRequest
  attributes:
    - email: string<email>;
    - password: string(6..);
end

// Response schemas
Entity LoginResponse
  attributes:
    - username: string;
    - token: string;
end

// ============================================================
// TRANSFORMATION ENTITIES (Computed/Enriched Data)
// ============================================================

// Transform and build payload for external user registration
Entity NewUserPayload(UserRegisterRequest)
  attributes:
    - user: object = {
        "username": trim(UserRegisterRequest.username),
        "password": UserRegisterRequest.password,
        "email": trim(UserRegisterRequest.email)
      };
end

// Transform login request
Entity LoginPayload(LoginRequest)
  attributes:
    - username: string = trim(LoginRequest.username);
    - password: string = trim(LoginRequest.password);
end

// Find matching user for login validation
Entity LoginCandidates(LoginPayload, UsersListSchema)
  attributes:
    - match: object = find(UsersListSchema.users, u ->
        (u["username"] == LoginPayload.username and u["password"] == LoginPayload.password)
      );
end

// Validated login data
Entity LoginValidated(LoginCandidates)
  attributes:
    - username: string = LoginCandidates.match["username"];
    - password: string = LoginCandidates.match["password"];
end

// Reset password transformation
Entity ResetPasswordPayload(ResetPasswordRequest, UsersListSchema)
  attributes:
    - user: object = find(UsersListSchema.users, u -> u["email"] == ResetPasswordRequest.email);
    - email: string = ResetPasswordRequest.email;
    - newPassword: string = ResetPasswordRequest.newPassword;
end

// ============================================================
// EXTERNAL SOURCES (Backend Services)
// ============================================================

Source<REST> UsersDB
  url: "http://dummydb:9000/db/users"
  verb: GET
  response:
    schema: UsersListSchema
end

Source<REST> UserRegisterExternal
  url: "http://dummydb:9000/db/users/register"
  verb: POST
  request:
    schema: NewUserPayload
  response:
    schema: StandardMessageResponse
end

Source<REST> UserLoginExternal
  url: "http://dummydb:9000/db/users/login"
  verb: POST
  request:
    schema: LoginValidated
  response:
    schema: LoginResponse
end

Source<REST> ResetPasswordExternal
  url: "http://dummydb:9000/db/users/reset-password"
  verb: PUT
  request:
    schema: ResetPasswordRequest
  response:
    schema: SuccessResponse
end

Source<REST> UserUpdateExternal
  url: "http://dummydb:9000/db/users/{id}"
  verb: PATCH
  request:
    schema: UserUpdateRequest
  response:
    schema: StandardMessageResponse
end

Source<REST> UserDeleteExternal
  url: "http://dummydb:9000/db/users/{id}"
  verb: DELETE
  response:
    schema: StandardMessageResponse
end

// ============================================================
// API ENDPOINTS (Public API)
// ============================================================

// --- USER LIST (GET) ---
APIEndpoint<REST> UserList
  path: "/api/users"
  verb: GET
  response:
    schema: UsersListSchema
end

// --- REGISTER ---
APIEndpoint<REST> UserRegister
  path: "/api/users/register"
  verb: POST
  request:
    schema: UserRegisterRequest
  response:
    schema: StandardMessageResponse
end

// --- LOGIN ---
APIEndpoint<REST> UserLogin
  path: "/api/users/login"
  verb: POST
  request:
    schema: LoginRequest
  response:
    schema: LoginResponse
end

// --- RESET PASSWORD ---
APIEndpoint<REST> ResetPassword
  path: "/api/users/reset-password"
  verb: PUT
  request:
    schema: ResetPasswordRequest
  response:
    schema: SuccessResponse
end

// --- UPDATE USER ---
APIEndpoint<REST> UserUpdate
  path: "/api/users/{id}/update"
  verb: PATCH
  parameters:
    path:
      - id: integer
  request:
    schema: UserUpdateRequest
  response:
    schema: StandardMessageResponse
end

// --- DELETE USER ---
APIEndpoint<REST> UserDelete
  path: "/api/users/{id}/delete"
  verb: DELETE
  parameters:
    path:
      - id: integer
  response:
    schema: StandardMessageResponse
end

// ============================================================
// UI Components
// ============================================================
Component<ActionForm> RegisterForm
  endpoint: UserRegister
  fields: [username, email, password]
  submitLabel: "Register"
end

Component<ActionForm> LoginForm
  endpoint: UserLogin
  fields: [username, password]
  submitLabel: "Login"
end

Component<ActionForm> ResetPasswordForm
  endpoint: ResetPassword
  fields: [email, newPassword]
  submitLabel: "Reset Password"
end

Component<ActionForm> UpdateUserForm
  endpoint: UserUpdate
  fields: [id, email, password]
  submitLabel: "Update User"
end

Component<ActionForm> DeleteUserForm
  endpoint: UserDelete
  fields: [id]
  submitLabel: "Delete User"
end

Component<Table> UserTable
  endpoint: UserList
  columns:
    - "id": integer
    - "username": string
    - "email": string<email>
    - "password": string
end
