Server WeatherServer
  host: "localhost"
  port: 8080
  cors: "http://localhost:3000"
  loglevel: debug
end

// --- External REST sources -------------------------------------
Source<REST> MeteoThess
  url: "https://api.open-meteo.com/v1/forecast?latitude=40.64&longitude=22.94&hourly=temperature_2m,relative_humidity_2m&timezone=auto"
  verb: GET
  response:
    schema: ThessHourly
end

Source<REST> MeteoLondon
  url: "https://api.open-meteo.com/v1/forecast?latitude=51.5072&longitude=-0.1276&hourly=temperature_2m,relative_humidity_2m&timezone=auto"
  verb: GET
  response:
    schema: LondonHourly
end

// --- Raw entities ----------------------------------------------
Entity ThessHourly
  attributes:
    - hourly: object = MeteoThess["hourly"];
end

Entity LondonHourly
  attributes:
    - hourly: object = MeteoLondon["hourly"];
end

// --- Row-shaped entity ---------------------------------
Entity WeatherCompare(ThessHourly, LondonHourly)
  attributes:
    - rows: array = map(
        zip(
          ThessHourly.hourly["time"],
          ThessHourly.hourly["temperature_2m"],
          LondonHourly.hourly["temperature_2m"],
          ThessHourly.hourly["relative_humidity_2m"],
          LondonHourly.hourly["relative_humidity_2m"]
        ),
        (row) -> {
          "time": row[0],
          "th_temp": row[1],
          "ln_temp": row[2],
          "th_hum": row[3],
          "ln_hum": row[4],
          "delta_temp": abs(row[1] - row[2]),
          "hotter_city": ("Thessaloniki" if row[1] > row[2] else "London"),
          "big_gap": (abs(row[1] - row[2]) >= 5),
          "th_is_comfy": ((abs(row[1] - 22) < 3) and (row[3] >= 30) and (row[3] <= 60)),
          "ln_is_comfy": ((abs(row[2] - 22) < 3) and (row[4] >= 30) and (row[4] <= 60))
        }
      );
end

// --- Simplified stats view (line chart) ------------------------
Entity WeatherStats(WeatherCompare)
  attributes:
    - rows: array = map(WeatherCompare.rows, row ->
          { "time": row["time"], "th_temp": row["th_temp"], "ln_temp": row["ln_temp"] }
        );
end

// --- Internal endpoints ----------------------------------------
APIEndpoint<REST> WeatherCompareAPI
  path: "/api/weather/compare"
  verb: GET
  response:
    schema: WeatherCompare
end

APIEndpoint<REST> WeatherStatsAPI
  path: "/api/weather/stats"
  verb: GET
  response:
    schema: WeatherStats
end

// --- Components ------------------------------------------------
Component<Table> WeatherTable
  endpoint: WeatherCompareAPI
  columns:
    - "time": string<time>
    - "th_temp": number
    - "ln_temp": number
    - "th_hum": number
    - "ln_hum": number
    - "delta_temp": number
    - "hotter_city": string
    - "big_gap": boolean
    - "th_is_comfy": boolean
    - "ln_is_comfy": boolean
end

Component<LineChart> TempComparison
  endpoint: WeatherStatsAPI
  rows: data.rows
  seriesLabels: ["Thessaloniki", "London"]
  xLabel: "Time"
  yLabel: "Temperature (Â°C)"
end
