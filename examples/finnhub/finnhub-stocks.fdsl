
Auth<apikey> UserAuth
  in: header
  name: "X-API-Key"
end

Role trader uses UserAuth

Auth<apikey> FinnhubSourceAuth
  in: query
  name: "token"
  secret: "FINNHUB_API_KEY"
end

Server FinnhubStockAPI
  host: "localhost"
  port: 8000
  cors: "*"
  loglevel: debug
end


Source<REST> QuoteAPI
  url: "https://finnhub.io/api/v1/quote"
  params: [symbol]
  operations: [read]
  auth: FinnhubSourceAuth
end


Source<REST> ProfileAPI
  url: "https://finnhub.io/api/v1/stock/profile2"
  params: [symbol]
  operations: [read]
  auth: FinnhubSourceAuth
end


Source<WS> FinnhubTradesWS
  channel: "wss://ws.finnhub.io"
  operations: [subscribe]
  auth: FinnhubSourceAuth
end

Entity RawQuote
  source: QuoteAPI
  attributes:
    - c: number @readonly;      
    - d: number @readonly;      
    - dp: number @readonly;     
    - h: number @readonly;       
    - l: number @readonly;     
    - o: number @readonly;  
    - pc: number @readonly;    
    - t: integer @readonly;  
  access: [trader]
end

Entity RawProfile
  source: ProfileAPI
  attributes:
    - ticker: string @readonly;
    - name: string @readonly;
    - country: string @readonly;
    - currency: string @readonly;
    - exchange: string @readonly;
    - ipo: string @readonly;
    - marketCapitalization: number @readonly;
    - shareOutstanding: number @readonly;
    - logo: string @readonly;
    - phone: string @readonly;
    - weburl: string @readonly;
    - finnhubIndustry: string @readonly;
  access: [trader]
end

// Combined stock info with calculated fields
Entity StockDashboard(RawQuote, RawProfile)
  attributes:
    - ticker: string = RawProfile.ticker;
    - companyName: string = RawProfile.name;
    - industry: string = RawProfile.finnhubIndustry;
    - exchange: string = RawProfile.exchange;
    - logoUrl: string = RawProfile.logo;

    - currentPrice: number = RawQuote.c;
    - priceChange: number = RawQuote.d;
    - percentChange: number = round(RawQuote.dp, 2);

    - priceFormatted: string = "$" + toString(round(RawQuote.c, 2));
    - changeFormatted: string = toString(round(RawQuote.d, 2)) + " (" + toString(round(RawQuote.dp, 2)) + "%)";

    - dayHigh: number = RawQuote.h;
    - dayLow: number = RawQuote.l;
    - dayRange: string = "$" + toString(round(RawQuote.l, 2)) + " - $" + toString(round(RawQuote.h, 2));

    - openPrice: number = RawQuote.o;
    - previousClose: number = RawQuote.pc;
    - marketCap: number = RawProfile.marketCapitalization;
    - marketCapFormatted: string = "$" + toString(round(RawProfile.marketCapitalization / 1000, 2)) + "B";

    - priceVsOpen: number = round(RawQuote.c - RawQuote.o, 2);
    - dayVolatility: number = round((RawQuote.h - RawQuote.l) / RawQuote.l * 100, 2);
    - isPositive: boolean = RawQuote.d >= 0;
    - trend: string = "up" if RawQuote.d > 0 else ("down" if RawQuote.d < 0 else "flat");
  access: [trader]
end

Entity RawTrade
  flow: inbound
  source: FinnhubTradesWS
  attributes:
    - s: string;         // Symbol (e.g., "AAPL")
    - p: number;         // Last trade price
    - v: number;         // Volume
    - t: integer<int64>; // Timestamp in milliseconds
  access: public
end

Entity LiveTrade(RawTrade)
  flow: inbound
  attributes:
    - symbol: string = RawTrade.s;
    - price: number = RawTrade.p;
    - volume: number = RawTrade.v;
    - timestamp: integer<int64> = RawTrade.t;
    - priceFormatted: string = "$" + toString(round(RawTrade.p, 2));
    - volumeFormatted: string = toString(toInt(RawTrade.v)) + " shares";
  access: public
end

Component<EntityCard> StockCard
  entity: StockDashboard
  type: rest
  fields: ["ticker", "companyName", "priceFormatted", "changeFormatted", "dayRange", "marketCapFormatted"]
  title: "Stock Dashboard"
  highlight: "priceFormatted"
end

Component<EntityCard> TradeCard
  entity: LiveTrade
  type: ws
  fields: ["symbol", "priceFormatted", "volumeFormatted"]
  title: "Live Trades"
  highlight: "priceFormatted"
end
