Server UserManagement
  host: "localhost"
  port: 8081
  cors: "http://localhost:5173"
  loglevel: debug
end

// READ: Get all users
Source<REST> ReadUsers
  url: "http://dummydb:9000/db/users"
  method: GET
  response:
    type: object
    entity: UsersListWrapper
end

Entity UsersListWrapper
  attributes:
    - users: array;
end

Entity LoginRequest
  attributes:
    - username: string(3..);
    - password: string(6..);
end

Entity LoginMatch(LoginRequest, UsersListWrapper)
  attributes:
    - user: object? = find(
        UsersListWrapper.users,
        u => u["username"] == LoginRequest.username and u["password"] == LoginRequest.password
      );
    - token: string = sha256(get(LoginMatch.user, "username", ""));
end

// Transform responses
Entity LoginResponse(LoginMatch)
  attributes:
    - token: string = LoginMatch.token;
    - username: string = get(LoginMatch.user, "username", "");
end

// Login (validates credentials in FDSL layer)
Endpoint<REST> Login
  path: "/api/auth/login"
  method: POST
  request:
    type: object
    entity: LoginRequest
  response:
    type: object
    entity: LoginResponse
  errors:
    - 401:
      condition: not LoginMatch.user
      message: "Invalid username or password"
end

