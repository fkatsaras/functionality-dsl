Server AuthServer
  host: "localhost"
  port: 8081
  cors: "http://localhost:5173"
  loglevel: debug
end

// ============================================================
// SCHEMAS (Pure Data Structures)
// ============================================================

// Raw response schemas from dummy service (reusable patterns)

Entity SuccessMessageResponse
  attributes:
    - ok: boolean;
    - message: string;
    - user: object?;
end

Entity LoginRawResponse
  attributes:
    - ok: boolean;
    - token: string;
end

Entity UsersListSchema
  attributes:
    - users: array;
end

// Request schemas (input validation)
Entity UserRegisterRequest
  attributes:
    - username: string(3..50);
    - password: string(6..);
    - email: string<email>;
end

Entity LoginRequest
  attributes:
    - username: string;
    - password: string;
end

Entity ResetPasswordRequest
  attributes:
    - email: string<email>;
    - newPassword: string(6..);
end

Entity UserUpdateRequest
  attributes:
    - email: string<email>;
    - password: string(6..);
end

// ============================================================
// TRANSFORMATION ENTITIES (Computed/Enriched Data)
// ============================================================

// Transform and build payload for external user registration
Entity NewUserPayload(UserRegisterRequest)
  attributes:
    - user: object = {
        "username": trim(UserRegisterRequest.username),
        "password": UserRegisterRequest.password,
        "email": trim(UserRegisterRequest.email)
      };
end

// Transform login request
Entity LoginPayload(LoginRequest)
  attributes:
    - username: string = trim(LoginRequest.username);
    - password: string = trim(LoginRequest.password);
end

// Find matching user for login validation
Entity LoginCandidates(LoginPayload, UsersListSchema)
  attributes:
    - match: object = find(UsersListSchema.users, u ->
        (u["username"] == LoginPayload.username and u["password"] == LoginPayload.password)
      );
end

// Validated login data
Entity LoginValidated(LoginCandidates)
  attributes:
    - username: string = LoginCandidates.match["username"];
    - password: string = LoginCandidates.match["password"];
end

// Reset password transformation
Entity ResetPasswordPayload(ResetPasswordRequest, UsersListSchema)
  attributes:
    - user: object = find(UsersListSchema.users, u -> u["email"] == ResetPasswordRequest.email);
    - email: string = ResetPasswordRequest.email;
    - newPassword: string = ResetPasswordRequest.newPassword;
end

// Response transformations - Convert raw responses to clean API responses
Entity RegisterResponse(SuccessMessageResponse)
  attributes:
    - id: integer = SuccessMessageResponse.user["id"];
    - message: string = SuccessMessageResponse.message;
end

Entity LoginResponse(LoginRawResponse)
  attributes:
    - token: string = LoginRawResponse.token;
end

Entity TransformedResponse(SuccessMessageResponse)
  attributes:
    - success: boolean = SuccessMessageResponse.ok;
    - message: string = SuccessMessageResponse.message;
end

// ============================================================
// EXTERNAL SOURCES (Backend Services)
// ============================================================

Source<REST> UsersDB
  url: "http://dummydb:9000/db/users"
  method: GET
  response:
    type: object
    entity: UsersListSchema
end

Source<REST> UserRegisterExternal
  url: "http://dummydb:9000/db/users/register"
  method: POST
  request:
    type: object
    entity: NewUserPayload
  response:
    type: object
    entity: SuccessMessageResponse
end

Source<REST> UserLoginExternal
  url: "http://dummydb:9000/db/users/login"
  method: POST
  request:
    type: object
    entity: LoginValidated
  response:
    type: object
    entity: LoginRawResponse
end

Source<REST> ResetPasswordExternal
  url: "http://dummydb:9000/db/users/reset-password"
  method: PUT
  request:
    type: object
    entity: ResetPasswordRequest
  response:
    type: object
    entity: SuccessMessageResponse
end

Source<REST> UserUpdateExternal
  url: "http://dummydb:9000/db/users/{id}"
  method: PATCH
  parameters:
    path:
      - id: integer = UserUpdate.id;
  request:
    type: object
    entity: UserUpdateRequest
  response:
    type: object
    entity: SuccessMessageResponse
end

Source<REST> UserDeleteExternal
  url: "http://dummydb:9000/db/users/{id}"
  method: DELETE
  parameters:
    path:
      - id: integer = UserDelete.id;
  response:
    type: object
    entity: SuccessMessageResponse
end

// ============================================================
// API ENDPOINTS (Public API)
// ============================================================

// --- USER LIST (GET) ---
Endpoint<REST> UserList
  path: "/api/users"
  method: GET
  response:
    type: object
    entity: UsersListSchema
end

// --- REGISTER ---
Endpoint<REST> UserRegister
  path: "/api/users/register"
  method: POST
  request:
    type: object
    entity: UserRegisterRequest
  response:
    type: object
    entity: RegisterResponse
end

// --- LOGIN ---
Endpoint<REST> UserLogin
  path: "/api/users/login"
  method: POST
  request:
    type: object
    entity: LoginRequest
  response:
    type: object
    entity: LoginResponse
end

// --- RESET PASSWORD ---
Endpoint<REST> ResetPassword
  path: "/api/users/reset-password"
  method: PUT
  request:
    type: object
    entity: ResetPasswordRequest
  response:
    type: object
    entity: TransformedResponse
end

// --- UPDATE USER ---
Endpoint<REST> UserUpdate
  path: "/api/users/{id}/update"
  method: PATCH
  parameters:
    path:
      - id: integer
  request:
    type: object
    entity: UserUpdateRequest
  response:
    type: object
    entity: TransformedResponse
end

// --- DELETE USER ---
Endpoint<REST> UserDelete
  path: "/api/users/{id}/delete"
  method: DELETE
  parameters:
    path:
      - id: integer
  response:
    type: object
    entity: TransformedResponse
end

// ============================================================
// UI Components
// ============================================================
Component<ActionForm> RegisterForm
  endpoint: UserRegister
  fields: [username, email, password]
  submitLabel: "Register"
end

Component<ActionForm> LoginForm
  endpoint: UserLogin
  fields: [username, password]
  submitLabel: "Login"
end

Component<ActionForm> ResetPasswordForm
  endpoint: ResetPassword
  fields: [email, newPassword]
  submitLabel: "Reset Password"
end

Component<ActionForm> UpdateUserForm
  endpoint: UserUpdate
  fields: [id, email, password]
  submitLabel: "Update User"
end

Component<ActionForm> DeleteUserForm
  endpoint: UserDelete
  fields: [id]
  submitLabel: "Delete User"
end

Component<Table> UserTable
  endpoint: UserList
  columns:
    - "id": integer
    - "username": string
    - "email": string<email>
    - "password": string
end
