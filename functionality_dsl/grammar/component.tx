import utils
import entity
import expression


// AttrRef remains, but will resolve via Component.endpoint.entity
AttrRef: 'data' '.' attr=[entity.Attribute] ;

Component:
    TableComponent
  | LineChartComponent
  | ActionFormComponent
  | GaugeComponent
  | InputComponent 
  | LiveViewComponent
;

TableComponent:
  'Component' '<' 'Table' '>' name=ID
  ('endpoint:' endpoint=[APIEndpointREST])?   // still binds to APIEndpoint<REST>/WS endpoint
  ('colNames:' colNames=StringList)?                   // explicit list of column names ['id','name',...]
  'end'
;

LineChartComponent:
  'Component' '<' 'LineChart' '>' name=ID
  ('endpoint:' endpoint=[APIEndpointWS])?
  (
    ('rows:' rows=AttrRef)?
    ('seriesLabels:' '[' seriesLabels+=STRING (',' seriesLabels+=STRING)* ']')?
    ('xLabel:' xLabel=STRING)?
    ('yLabel:' yLabel=STRING)?
  )#
  'end'
;

GaugeComponent:
  'Component' '<' 'Gauge' '>' name=ID
  ('endpoint:' endpoint=[APIEndpointWS])?
  ( ('value:' value=AttrRef)? ('min:' min=FLOAT)? ('max:' max=FLOAT)? ('label:' label=STRING)? ('unit:'  unit=STRING)? )#
  'end'
;

// ActionForm should *not* hit External anymore; point to APIEndpoint<REST>
ActionFormComponent:
  'Component' '<' 'ActionForm' '>' name=ID
  (
    'endpoint:' endpoint=[APIEndpointREST]
    ('fields:' '[' fields+=ID (',' fields+=ID)* ']')?
    ('pathKey:' pathKey=ID)?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

InputComponent:
  'Component' '<' 'Input' '>' name=ID
  'endpoint' ':' endpoint=[APIEndpointWS]
  ( ('label:' label=STRING)
  | ('placeholder:' placeholder=STRING)
  | ('initial:' initial=STRING)
  | ('submitLabel:' submitLabel=STRING)
  )*
  'end'
;

LiveViewComponent:
  'Component' '<' 'LiveView' '>' name=ID
  'endpoint' ':' endpoint=[APIEndpointWS]
  'fields:' fields=StringList
  ( ('label:' label=STRING)
  )*
  'end'
;