import utils
import entity
import expression


// AttrRef remains, but will resolve via Component.endpoint.entity
AttrRef: 'data' '.' attr=[entity.Attribute] ;

Component:
    TableComponent
  | ChartComponent
  | LiveChartComponent
  | GaugeComponent
  | ToggleComponent
  | TogglePanelComponent
  | CameraComponent
  | EntityCardComponent
  | PieChartComponent
;

// Column definition with type specification
// Simple type string format: "colname": typename or "colname": typename<format>
// Supported formats: time, date, email, uri, image, binary, etc.
ColumnDef:
  name=STRING ':' typename=ID ('<' format=ID '>')? constraint=TypeConstraintCol? nullable='?'?
;

TypeConstraintCol:
  '(' rangeCol=RangeExprCol ')'
;

RangeExprCol:
    min=NumberCol '..' max=NumberCol
  | min=NumberCol '..'
  | '..' max=NumberCol
  | exact=NumberCol
;

NumberCol: FLOAT | INT;

TableComponent:
  'Component' '<' 'Table' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('arrayField:' arrayField=STRING)?                    // field containing array of items (auto-detected if single array<T>)
    ('keyField:' keyField=STRING)?                        // unique key field in items (auto-detected: 'id' or first field)
    (
        ('colNames:' colNames=StringList)                 // legacy: explicit list of column names ['id','name',...]
      | ('columns:' '-' columns+=ColumnDef ('-' columns+=ColumnDef)*)  // new: typed columns
    )?
  )#
  'end'
;

// Typed label for chart axes
TypedLabel:
  typename=ID ('<' format=ID '>')? label=STRING
;

ChartComponent:
  'Component' '<' 'Chart' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('values:' values=ID)?
    ('xField:' xField=ID)?
    ('yField:' yField=ID)?
    ('seriesLabels:' '[' seriesLabels+=STRING (',' seriesLabels+=STRING)* ']')?
    ('xLabel:' xLabel=STRING)?
    ('yLabel:' yLabel=STRING)?
    ('refreshMs:' refreshMs=INT)?
    ('windowSize:' windowSize=INT)?
    ('height:' height=INT)?
  )#
  'end'
;

LiveChartComponent:
  'Component' '<' 'LiveChart' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('values:' values=ID)?
    ('xField:' xField=ID)?
    ('yField:' yField=ID)?
    ('seriesLabels:' '[' seriesLabels+=STRING (',' seriesLabels+=STRING)* ']')?
    ('yScale:' yScale=FLOAT)?
    ('xLabel:' xLabel=STRING)?
    ('yLabel:' yLabel=STRING)?
    ('windowSize:' windowSize=INT)?
    ('height:' height=INT)?
    ('fullWidth:' fullWidth=BOOL)?
  )#
  'end'
;

GaugeComponent:
  'Component' '<' 'Gauge' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  ( ('value:' value=STRING)? ('min:' min=FLOAT)? ('max:' max=FLOAT)? ('label:' label=STRING)? ('unit:'  unit=STRING)? )#
  'end'
;

ToggleComponent:
  'Component' '<' 'Toggle' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('label:' label=STRING)?
    ('onLabel:' onLabel=STRING)?
    ('offLabel:' offLabel=STRING)?
    ('field:' field=STRING)?
    ('refreshMs:' refreshMs=INT)?
  )#
  'end'
;

// ToggleField defines a single toggle in a TogglePanel
ToggleField:
  'field:' field=STRING 'label:' label=STRING
;

// TogglePanel groups multiple toggles for the same entity in one card
TogglePanelComponent:
  'Component' '<' 'TogglePanel' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('toggles:' '-' toggles+=ToggleField ('-' toggles+=ToggleField)*)?
    ('title:' title=STRING)?
    ('refreshMs:' refreshMs=INT)?
  )#
  'end'
;

CameraComponent:
  'Component' '<' 'Camera' '>' name=ID
  'entity' ':' entity_ref=[entity.Entity]
  ( ('label:' label=STRING)
  )*
  'end'
;

// EntityCard - displays entity scalar fields (non-binary, non-array, non-object)
// type: "rest" for REST polling, "ws" for WebSocket streaming
EntityCardComponent:
  'Component' '<' 'EntityCard' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  'type:' cardType=EntityCardType
  (
    ('fields:' '[' fields+=STRING (',' fields+=STRING)* ']')?
    ('title:' title=STRING)?
    ('highlight:' highlight=STRING)?
    ('refreshMs:' refreshMs=INT)?
  )#
  'end'
;

EntityCardType:
  'rest' | 'ws'
;

// SliceField defines a mapping from entity field to pie chart slice
SliceField:
  'field:' field=STRING 'label:' label=STRING 'color:' color=STRING
;

PieChartComponent:
  'Component' '<' 'PieChart' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('slices:' '-' slices+=SliceField ('-' slices+=SliceField)*)?
    ('title:' title=STRING)?
    ('size:' size=INT)?
    ('refreshMs:' refreshMs=INT)?
  )#
  'end'
;

// =============================================================================
// DEPRECATED COMPONENTS
// Moved to component_deprecated.tx - preserved for potential future use
// =============================================================================
