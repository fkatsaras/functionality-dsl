import utils
import entity
import expression


// AttrRef remains, but will resolve via Component.endpoint.entity
AttrRef: 'data' '.' attr=[entity.Attribute] ;

Component:
    TableComponent
  | LiveTableComponent
  | ChartComponent
  | LiveChartComponent
  | ActionFormComponent
  | QueryFormComponent
  | TextFormComponent
  | FileUploadFormComponent
  | GaugeComponent
  | InputComponent
  | LiveViewComponent
  | LiveMetricsComponent
  | MapComponent
  | ToggleComponent
  | ObjectViewComponent
  | CameraComponent
  | DownloadFormComponent
  | MetricComponent
  | DataCardComponent
  | PieChartComponent
  | LivePieChartComponent
  | BarChartComponent
  | ProgressComponent
  | LiveProgressComponent
  | AlertComponent
  | LiveAlertComponent
  | PublishFormComponent
;

// Column definition with type specification
// Simple type string format: "colname": typename or "colname": typename<format>
// Supported formats: time, date, email, uri, image, binary, etc.
ColumnDef:
  name=STRING ':' typename=ID ('<' format=ID '>')? constraint=TypeConstraintCol? nullable='?'?
;

TypeConstraintCol:
  '(' rangeCol=RangeExprCol ')'
;

RangeExprCol:
    min=NumberCol '..' max=NumberCol
  | min=NumberCol '..'
  | '..' max=NumberCol
  | exact=NumberCol
;

NumberCol: FLOAT | INT;

TableComponent:
  'Component' '<' 'Table' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
      ('colNames:' colNames=StringList)                   // legacy: explicit list of column names ['id','name',...]
    | ('columns:' '-' columns+=ColumnDef ('-' columns+=ColumnDef)*)  // new: typed columns
  )?
  'end'
;

LiveTableComponent:
  'Component' '<' 'LiveTable' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  ('arrayField:' arrayField=STRING)?
  'keyField:' keyField=STRING
  (
      ('colNames:' colNames=StringList)
    | ('columns:' '-' columns+=ColumnDef ('-' columns+=ColumnDef)*)
  )?
  ('label:' label=STRING)?
  ('maxRows:' maxRows=INT)?
  'end'
;

ObjectViewComponent:
  'Component' '<' 'ObjectView' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  'fields:' fields=StringList                // explicit list of attributes to display
  ('label:' label=STRING)?
  'end'
;

// Typed label for chart axes
TypedLabel:
  typename=ID ('<' format=ID '>')? label=STRING
;

ChartComponent:
  'Component' '<' 'Chart' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('values:' values=ID)?
    ('xField:' xField=ID)?
    ('yField:' yField=ID)?
    ('seriesLabels:' '[' seriesLabels+=STRING (',' seriesLabels+=STRING)* ']')?
    ('xLabel:' xLabel=STRING)?
    ('yLabel:' yLabel=STRING)?
    ('refreshMs:' refreshMs=INT)?
    ('windowSize:' windowSize=INT)?
    ('height:' height=INT)?
  )#
  'end'
;

LiveChartComponent:
  'Component' '<' 'LiveChart' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('values:' values=ID)?
    ('xField:' xField=ID)?
    ('yField:' yField=ID)?
    ('seriesLabels:' '[' seriesLabels+=STRING (',' seriesLabels+=STRING)* ']')?
    ('yScale:' yScale=FLOAT)?
    ('xLabel:' xLabel=STRING)?
    ('yLabel:' yLabel=STRING)?
    ('windowSize:' windowSize=INT)?
    ('height:' height=INT)?
  )#
  'end'
;

GaugeComponent:
  'Component' '<' 'Gauge' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  ( ('value:' value=STRING)? ('min:' min=FLOAT)? ('max:' max=FLOAT)? ('label:' label=STRING)? ('unit:'  unit=STRING)? )#
  'end'
;

// ActionForm should *not* hit External anymore; point to Endpoint<REST>
ActionFormComponent:
  'Component' '<' 'ActionForm' '>' name=ID
  (
    'entity:' entity=[Entity]
    ('operation:' operation=ActionOperation)?
    ('fields:' '[' fields+=ID (',' fields+=ID)* ']')?
    ('pathKey:' pathKey=ID)?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

ActionOperation:
  'create' | 'update' | 'delete'
;

// QueryForm for GET requests with query parameters
QueryFormComponent:
  'Component' '<' 'QueryForm' '>' name=ID
  (
    'entity:' entity_ref=[entity.Entity]
    ('fields:' '[' fields+=ID (',' fields+=ID)* ']')?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

TextFormComponent:
  'Component' '<' 'TextForm' '>' name=ID
  (
    'entity:' entity_ref=[entity.Entity]
    ('label:' label=STRING)?
    ('placeholder:' placeholder=STRING)?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

FileUploadFormComponent:
  'Component' '<' 'FileUploadForm' '>' name=ID
  (
    'entity:' entity_ref=[entity.Entity]
    ('label:' label=STRING)?
    ('accept:' accept=STRING)?
    ('maxSize:' maxSize=INT)?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

InputComponent:
  'Component' '<' 'Input' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  ( ('label:' label=STRING)
  | ('placeholder:' placeholder=STRING)
  | ('initial:' initial=STRING)
  | ('submitLabel:' submitLabel=STRING)
  )*
  'end'
;

LiveViewComponent:
  'Component' '<' 'LiveView' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  'fields:' fields=StringList
  ( ('label:' label=STRING)
  )*
  'end'
;

ToggleComponent:
  'Component' '<' 'Toggle' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('label:' label=STRING)?
    ('onLabel:' onLabel=STRING)?
    ('offLabel:' offLabel=STRING)?
    ('field:' field=STRING)?
    ('refreshMs:' refreshMs=INT)?
  )#
  'end'
;

CameraComponent:
  'Component' '<' 'Camera' '>' name=ID
  'entity' ':' entity_ref=[entity.Entity]
  ( ('label:' label=STRING)
  )*
  'end'
;

DownloadFormComponent:
  'Component' '<' 'DownloadForm' '>' name=ID
  (
    'entity:' entity_ref=[entity.Entity]
    ('filename:' filename=STRING)?
    ('buttonText:' buttonText=STRING)?
    ('params:' '[' params+=ID (',' params+=ID)* ']')?
    ('autoDownload:' autoDownload=BOOL)?
    ('showPreview:' showPreview=BOOL)?
  )#
  'end'
;

LiveMetricsComponent:
  'Component' '<' 'LiveMetrics' '>' name=ID
  'entity' ':' entity_ref=[entity.Entity]
  'metrics:' '[' metrics+=STRING (',' metrics+=STRING)* ']'
  ( ('label:' label=STRING)
  )*
  'end'
;

MapComponent:
  'Component' '<' 'Map' '>' name=ID
  'entity' ':' entity_ref=[entity.Entity]
  'warehouseLat:' warehouseLat=FLOAT
  'warehouseLon:' warehouseLon=FLOAT
  (
    ('deliveriesKey:' deliveriesKey=STRING)?
    ('driversKey:' driversKey=STRING)?
    ('label:' label=STRING)?
    ('width:' width=INT)?
    ('height:' height=INT)?
  )#
  'end'
;

MetricComponent:
  'Component' '<' 'Metric' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('field:' field=STRING)?
    ('label:' label=STRING)?
    ('format:' format=STRING)?
    ('refreshMs:' refreshMs=INT)?
  )#
  'end'
;

DataCardComponent:
  'Component' '<' 'DataCard' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('fields:' '[' fields+=STRING (',' fields+=STRING)* ']')?
    ('title:' title=STRING)?
    ('highlight:' highlight=STRING)?
    ('refreshMs:' refreshMs=INT)?
  )#
  'end'
;

// SliceField defines a mapping from entity field to pie chart slice
SliceField:
  'field:' field=STRING 'label:' label=STRING 'color:' color=STRING
;

PieChartComponent:
  'Component' '<' 'PieChart' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('slices:' '-' slices+=SliceField ('-' slices+=SliceField)*)?
    ('title:' title=STRING)?
    ('size:' size=INT)?
    ('refreshMs:' refreshMs=INT)?
  )#
  'end'
;

LivePieChartComponent:
  'Component' '<' 'LivePieChart' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('slices:' '-' slices+=SliceField ('-' slices+=SliceField)*)?
    ('title:' title=STRING)?
    ('size:' size=INT)?
  )#
  'end'
;

// BarField defines a mapping from entity field to bar chart bar
BarField:
  'field:' field=STRING 'label:' label=STRING ('color:' color=STRING)?
;

BarChartComponent:
  'Component' '<' 'BarChart' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('bars:' '-' bars+=BarField ('-' bars+=BarField)*)?
    ('title:' title=STRING)?
    ('xLabel:' xLabel=STRING)?
    ('yLabel:' yLabel=STRING)?
    ('height:' height=INT)?
    ('width:' width=INT)?
    ('refreshMs:' refreshMs=INT)?
  )#
  'end'
;

ProgressComponent:
  'Component' '<' 'Progress' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('field:' field=STRING)?
    ('min:' min=FLOAT)?
    ('max:' max=FLOAT)?
    ('threshold:' threshold=FLOAT)?
    ('label:' label=STRING)?
    ('refreshMs:' refreshMs=INT)?
  )#
  'end'
;

LiveProgressComponent:
  'Component' '<' 'LiveProgress' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('field:' field=STRING)?
    ('min:' min=FLOAT)?
    ('max:' max=FLOAT)?
    ('threshold:' threshold=FLOAT)?
    ('label:' label=STRING)?
  )#
  'end'
;

AlertComponent:
  'Component' '<' 'Alert' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('condition:' condition=STRING)?
    ('message:' message=STRING)?
    ('severity:' severity=STRING)?
    ('refreshMs:' refreshMs=INT)?
  )#
  'end'
;

LiveAlertComponent:
  'Component' '<' 'LiveAlert' '>' name=ID
  'entity:' entity_ref=[entity.Entity]
  (
    ('condition:' condition=STRING)?
    ('message:' message=STRING)?
    ('severity:' severity=STRING)?
  )#
  'end'
;

// PublishForm for outbound WebSocket entities - sends JSON payloads
PublishFormComponent:
  'Component' '<' 'PublishForm' '>' name=ID
  (
    'entity:' entity_ref=[entity.Entity]
    ('fields:' '[' fields+=ID (',' fields+=ID)* ']')?
    ('submitLabel:' submitLabel=STRING)?
    ('label:' label=STRING)?
  )#
  'end'
;

