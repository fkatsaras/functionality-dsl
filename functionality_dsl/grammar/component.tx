import utils
import entity
import expression


// AttrRef remains, but will resolve via Component.endpoint.entity
AttrRef: 'data' '.' attr=[entity.Attribute] ;

Component:
    TableComponent
  | LineChartComponent
  | ActionFormComponent
  | GaugeComponent
  | InputComponent 
  | LiveViewComponent
;

TableComponent:
  'Component' '<' 'Table' '>' name=ID
  ('endpoint:' endpoint=[InternalRESTEndpoint])?   // still binds to InternalREST/WS endpoint
  ('colNames:' colNames=StringList)?                   // explicit list of column names ['id','name',...]
  'end'
;

LineChartComponent:
  'Component' '<' 'LineChart' '>' name=ID
  ('endpoint:' endpoint=[InternalRESTEndpoint])?
  (
    ('rows:' rows=AttrRef)?
    ('seriesLabels:' '[' seriesLabels+=STRING (',' seriesLabels+=STRING)* ']')?
    ('xLabel:' xLabel=STRING)?
    ('yLabel:' yLabel=STRING)?
  )#
  'end'
;

GaugeComponent:
  'Component' '<' 'Gauge' '>' name=ID
  ('endpoint:' endpoint=[InternalWSEndpoint])?
  ( ('value:' value=AttrRef)? ('min:' min=FLOAT)? ('max:' max=FLOAT)? ('label:' label=STRING)? ('unit:'  unit=STRING)? )#
  'end'
;

// ActionForm should *not* hit External anymore; point to InternalREST
ActionFormComponent:
  'Component' '<' 'ActionForm' '>' name=ID
  (
    'endpoint:' endpoint=[InternalRESTEndpoint]
    ('fields:' '[' fields+=ID (',' fields+=ID)* ']')?
    ('pathKey:' pathKey=ID)?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

InputComponent:
  'Component' '<' 'Input' '>' name=ID
  'endpoint' ':' endpoint=[InternalWSEndpoint]
  ( ('label:' label=STRING)
  | ('placeholder:' placeholder=STRING)
  | ('initial:' initial=STRING)
  | ('submitLabel:' submitLabel=STRING)
  )*
  'end'
;

LiveViewComponent:
  'Component' '<' 'LiveView' '>' name=ID
  'endpoint' ':' endpoint=[InternalWSEndpoint]
  'fields:' fields=StringList
  ( ('label:' label=STRING)
  )*
  'end'
;