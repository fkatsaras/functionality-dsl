import utils
import entity
import endpoints
import integration
import expression

// AttrRef remains, but will resolve via Component.endpoint.entity
AttrRef: 'data' '.' attr=[entity.Attribute] ;

Component:
    TableComponent
  | LineChartComponent
  | ActionFormComponent
  | GaugeComponent
  | PlotComponent
;

TableComponent:
  'Component' '<' 'Table' '>' name=ID
  ('endpoint:' endpoint=[InternalEndpoint])?   // still binds to InternalREST/WS endpoint
  ('colNames:' colNames=StringList)?                   // explicit list of column names ['id','name',...]
  'end'
;

LineChartComponent:
  'Component' '<' 'LineChart' '>' name=ID
  ('endpoint:' endpoint=[InternalEndpoint])?
  (
    ('rows:' rows=AttrRef)?
    ('seriesLabels:' '[' seriesLabels+=STRING (',' seriesLabels+=STRING)* ']')?
    ('xLabel:' xLabel=STRING)?
    ('yLabel:' yLabel=STRING)?
  )#
  'end'
;

GaugeComponent:
  'Component' '<' 'Gauge' '>' name=ID
  ('endpoint:' endpoint=[InternalEndpoint])?
  ( ('value:' value=AttrRef)? ('min:' min=FLOAT)? ('max:' max=FLOAT)? ('label:' label=STRING)? ('unit:'  unit=STRING)? )#
  'end'
;

// ActionForm should *not* hit External anymore; point to InternalREST
ActionFormComponent:
  'Component' '<' 'ActionForm' '>' name=ID
  (
    'action:' action=[ExternalRESTEndpoint]
    ('fields:' '[' fields+=ID (',' fields+=ID)* ']')?
    ('pathKey:' pathKey=ID)?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

PlotComponent:
  'Component' '<' 'Plot' '>' name=ID
  ('endpoint:' endpoint=[InternalEndpoint])?
  ( ('x:' x=AttrRef)? ('y:' y=AttrRef)? ('xLabel:' xLabel=STRING)? ('yLabel:' yLabel=STRING)? )#
  'end'
;
