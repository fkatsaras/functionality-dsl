import utils
import entity
import expression


// AttrRef remains, but will resolve via Component.endpoint.entity
AttrRef: 'data' '.' attr=[entity.Attribute] ;

Component:
    TableComponent
  | LiveTableComponent
  | ChartComponent
  | LiveChartComponent
  | ActionFormComponent
  | QueryFormComponent
  | TextFormComponent
  | FileUploadFormComponent
  | GaugeComponent
  | InputComponent
  | LiveViewComponent
  | ToggleComponent
  | ObjectViewComponent
  | CameraComponent
  | DownloadFormComponent
;

// Column definition with type specification
// Simple type string format: "colname": typename or "colname": typename<format>
// Supported formats: time, date, email, uri, image, binary, etc.
ColumnDef:
  name=STRING ':' typename=ID ('<' format=ID '>')? constraint=TypeConstraintCol? nullable='?'?
;

TypeConstraintCol:
  '(' rangeCol=RangeExprCol ')'
;

RangeExprCol:
    min=NumberCol '..' max=NumberCol
  | min=NumberCol '..'
  | '..' max=NumberCol
  | exact=NumberCol
;

NumberCol: FLOAT | INT;

TableComponent:
  'Component' '<' 'Table' '>' name=ID
  ('endpoint:' endpoint=[entity.Endpoint])?
  (
      ('colNames:' colNames=StringList)                   // legacy: explicit list of column names ['id','name',...]
    | ('columns:' '-' columns+=ColumnDef ('-' columns+=ColumnDef)*)  // new: typed columns
  )?
  'end'
;

LiveTableComponent:
  'Component' '<' 'LiveTable' '>' name=ID
  'endpoint:' endpoint=[EndpointWS]
  ('arrayField:' arrayField=STRING)?
  'keyField:' keyField=STRING
  (
      ('colNames:' colNames=StringList)
    | ('columns:' '-' columns+=ColumnDef ('-' columns+=ColumnDef)*)
  )?
  ('label:' label=STRING)?
  ('maxRows:' maxRows=INT)?
  'end'
;

ObjectViewComponent:
  'Component' '<' 'ObjectView' '>' name=ID
  'endpoint:' endpoint=[entity.Endpoint]
  'fields:' fields=StringList                // explicit list of attributes to display
  ('label:' label=STRING)?
  'end'
;

// Typed label for chart axes
TypedLabel:
  typename=ID ('<' format=ID '>')? label=STRING
;

ChartComponent:
  'Component' '<' 'Chart' '>' name=ID
  'endpoint:' endpoint=[EndpointREST]
  (
    ('values:' values=ID)?
    ('seriesLabels:' '[' seriesLabels+=STRING (',' seriesLabels+=STRING)* ']')?
    ('xLabel:' xLabel=TypedLabel)?
    ('yLabel:' yLabel=TypedLabel)?
    ('refreshMs:' refreshMs=INT)?
    ('windowSize:' windowSize=INT)?
    ('height:' height=INT)?
  )#
  'end'
;

LiveChartComponent:
  'Component' '<' 'LiveChart' '>' name=ID
  'endpoint:' endpoint=[EndpointWS]
  (
    ('values:' values=ID)?
    ('seriesLabels:' '[' seriesLabels+=STRING (',' seriesLabels+=STRING)* ']')?
    ('xLabel:' xLabel=TypedLabel)?
    ('yLabel:' yLabel=TypedLabel)?
    ('windowSize:' windowSize=INT)?
    ('height:' height=INT)?
  )#
  'end'
;

GaugeComponent:
  'Component' '<' 'Gauge' '>' name=ID
  ('endpoint:' endpoint=[entity.Endpoint])?
  ( ('value:' value=AttrRef)? ('min:' min=FLOAT)? ('max:' max=FLOAT)? ('label:' label=STRING)? ('unit:'  unit=STRING)? )#
  'end'
;

// ActionForm should *not* hit External anymore; point to Endpoint<REST>
ActionFormComponent:
  'Component' '<' 'ActionForm' '>' name=ID
  (
    'endpoint:' endpoint=[EndpointREST]
    ('fields:' '[' fields+=ID (',' fields+=ID)* ']')?
    ('pathKey:' pathKey=ID)?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

// QueryForm for GET requests with query parameters
QueryFormComponent:
  'Component' '<' 'QueryForm' '>' name=ID
  (
    'endpoint:' endpoint=[EndpointREST]
    ('fields:' '[' fields+=ID (',' fields+=ID)* ']')?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

TextFormComponent:
  'Component' '<' 'TextForm' '>' name=ID
  (
    'endpoint:' endpoint=[EndpointREST]
    ('label:' label=STRING)?
    ('placeholder:' placeholder=STRING)?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

FileUploadFormComponent:
  'Component' '<' 'FileUploadForm' '>' name=ID
  (
    'endpoint:' endpoint=[EndpointREST]
    ('label:' label=STRING)?
    ('accept:' accept=STRING)?
    ('maxSize:' maxSize=INT)?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

InputComponent:
  'Component' '<' 'Input' '>' name=ID
  'endpoint' ':' endpoint=[EndpointWS]
  ( ('label:' label=STRING)
  | ('placeholder:' placeholder=STRING)
  | ('initial:' initial=STRING)
  | ('submitLabel:' submitLabel=STRING)
  )*
  'end'
;

LiveViewComponent:
  'Component' '<' 'LiveView' '>' name=ID
  'endpoint' ':' endpoint=[EndpointWS]
  'fields:' fields=StringList
  ( ('label:' label=STRING)
  )*
  'end'
;

ToggleComponent:
  'Component' '<' 'Toggle' '>' name=ID
  'endpoint' ':' endpoint=[EndpointREST]
  ( ('label:' label=STRING)
  | ('onLabel:' onLabel=STRING)
  | ('offLabel:' offLabel=STRING)
  | ('field:' field=STRING)
  )*
  'end'
;

CameraComponent:
  'Component' '<' 'Camera' '>' name=ID
  'endpoint' ':' endpoint=[EndpointWS]
  ( ('label:' label=STRING)
  )*
  'end'
;

DownloadFormComponent:
  'Component' '<' 'DownloadForm' '>' name=ID
  (
    'endpoint:' endpoint=[EndpointREST]
    ('filename:' filename=STRING)?
    ('buttonText:' buttonText=STRING)?
    ('params:' '[' params+=ID (',' params+=ID)* ']')?
    ('autoDownload:' autoDownload=BOOL)?
    ('showPreview:' showPreview=BOOL)?
  )#
  'end'
;

