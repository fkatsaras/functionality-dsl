import utils
import entity
import endpoints

// ---------- attribute reference tied to the component's entity ----------
AttrRef:
  'data' '.' attr=[entity.Attribute]
;

// ---------- COMPONENTS (strictly typed, no generic props) ----------
Component:
    TableComponent
  | LineChartComponent
  | ActionFormComponent
  | GaugeComponent
  | PlotComponent 
;

TableComponent:
  'Component' '<' 'Table' '>' name=ID
  ('entity:' entity=[Entity])?
  (
    ('columns:' '[' columns+=AttrRef (',' columns+=AttrRef)* ']')?
    ('primaryKey:' (primaryKey=AttrRef | primaryKey_str=STRING))?
  )#
  'end'
;

LineChartComponent:
  'Component' '<' 'LineChart' '>' name=ID
  ('entity:' entity=[Entity])?
  (
    ('x:' x=AttrRef)?
    ('y:' '[' y+=AttrRef (',' y+=AttrRef)* ']')?
    ('xLabel:' xLabel=STRING)?
    ('yLabel:' yLabel=STRING)?
  )#
  'end'
;

GaugeComponent:
  'Component' '<' 'Gauge' '>' name=ID
  ('entity:' entity=[Entity])?
  (
    ('value:' value=AttrRef)?
    ('min:'   min=FLOAT)?
    ('max:'   max=FLOAT)?
    ('label:' label=STRING)?
    ('unit:'  unit=STRING)?
  )#
  'end'
;

ActionFormComponent:
  'Component' '<' 'ActionForm' '>' name=ID
  (
    'action:' action=[RESTEndpoint]
    ('fields:' '[' fields+=ID (',' fields+=ID)* ']')?
    ('pathKey:' pathKey=ID)?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

PlotComponent:
  'Component' '<' 'Plot' '>' name=ID
  ('entity:' entity=[Entity])?
  (
    ('x:' x=AttrRef)?           // expects: list<int|float> attribute key
    ('y:' y=AttrRef)?           // expects: list<int|float> attribute key
    ('xLabel:' xLabel=STRING)?
    ('yLabel:' yLabel=STRING)?
  )#
  'end'
;

