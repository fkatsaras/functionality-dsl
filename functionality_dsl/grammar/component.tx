import utils
import entity
import expression


// AttrRef remains, but will resolve via Component.endpoint.entity
AttrRef: 'data' '.' attr=[entity.Attribute] ;

Component:
    TableComponent
  | LineChartComponent
  | ActionFormComponent
  | GaugeComponent
  | InputComponent
  | LiveViewComponent
  | ToggleComponent
  | ObjectViewComponent
;

// Column definition with type specification
// Simple type string format: "colname": typename or "colname": typename<format>
// Supported formats: time, date, email, uri, image, binary, etc.
ColumnDef:
  name=STRING ':' typename=ID ('<' format=ID '>')? constraint=TypeConstraintCol? nullable='?'?
;

TypeConstraintCol:
  '(' rangeCol=RangeExprCol ')'
;

RangeExprCol:
    min=NumberCol '..' max=NumberCol
  | min=NumberCol '..'
  | '..' max=NumberCol
  | exact=NumberCol
;

NumberCol: FLOAT | INT;

TableComponent:
  'Component' '<' 'Table' '>' name=ID
  ('endpoint:' endpoint=[entity.Endpoint])?
  (
      ('colNames:' colNames=StringList)                   // legacy: explicit list of column names ['id','name',...]
    | ('columns:' '-' columns+=ColumnDef ('-' columns+=ColumnDef)*)  // new: typed columns
  )?
  'end'
;

ObjectViewComponent:
  'Component' '<' 'ObjectView' '>' name=ID
  'endpoint:' endpoint=[entity.Endpoint]
  'fields:' fields=StringList                // explicit list of attributes to display
  ('label:' label=STRING)?
  'end'
;

LineChartComponent:
  'Component' '<' 'LineChart' '>' name=ID
  ('endpoint:' endpoint=[entity.Endpoint])?
  (
    ('rows:' rows=AttrRef)?
    ('seriesLabels:' '[' seriesLabels+=STRING (',' seriesLabels+=STRING)* ']')?
    ('xLabel:' xLabel=STRING)?
    ('yLabel:' yLabel=STRING)?
  )#
  'end'
;

GaugeComponent:
  'Component' '<' 'Gauge' '>' name=ID
  ('endpoint:' endpoint=[entity.Endpoint])?
  ( ('value:' value=AttrRef)? ('min:' min=FLOAT)? ('max:' max=FLOAT)? ('label:' label=STRING)? ('unit:'  unit=STRING)? )#
  'end'
;

// ActionForm should *not* hit External anymore; point to APIEndpoint<REST>
ActionFormComponent:
  'Component' '<' 'ActionForm' '>' name=ID
  (
    'endpoint:' endpoint=[APIEndpointREST]
    ('fields:' '[' fields+=ID (',' fields+=ID)* ']')?
    ('pathKey:' pathKey=ID)?
    ('submitLabel:' submitLabel=STRING)?
  )#
  'end'
;

InputComponent:
  'Component' '<' 'Input' '>' name=ID
  'endpoint' ':' endpoint=[APIEndpointWS]
  ( ('label:' label=STRING)
  | ('placeholder:' placeholder=STRING)
  | ('initial:' initial=STRING)
  | ('submitLabel:' submitLabel=STRING)
  )*
  'end'
;

LiveViewComponent:
  'Component' '<' 'LiveView' '>' name=ID
  'endpoint' ':' endpoint=[APIEndpointWS]
  'fields:' fields=StringList
  ( ('label:' label=STRING)
  )*
  'end'
;

ToggleComponent:
  'Component' '<' 'Toggle' '>' name=ID
  'endpoint' ':' endpoint=[APIEndpointREST]
  ( ('label:' label=STRING)
  | ('onLabel:' onLabel=STRING)
  | ('offLabel:' offLabel=STRING)
  | ('field:' field=STRING)
  )*
  'end'
;
