import utils
import expression

AttrType:
  'string' | 'int' | 'float' | 'number' | 'bool' | 'datetime' | 'uuid' | 'dict' | 'list'
;

AttrValue:
  STRING | FLOAT | INT | Bool
;

Attribute:
    ComputedAttribute | SchemaAttribute  
;

SchemaAttribute:
  name=ID ':' type=AttrType (optional?='?')?
  ('default=' default=AttrValue)?
;

ComputedAttribute:
  name=ID ':' type=AttrType (optional?='?')? '=' expr=Expr ';'
;

Input:
  alias=ID ':' target=[Entity]
;

Entity:
  'Entity' name=ID
  ('(' parents+=[Entity] (',' parents+=[Entity])* ')')?
  (
    ('source:' source=[Source])?
    ('attributes:' '-' attributes+=Attribute ('-' attributes+=Attribute)*)?
  )#
  'end'
;


// Entity sources / targets -------------------
Source:
    InternalRESTEndpoint
  | InternalWSEndpoint
  | ExternalRESTEndpoint
  | ExternalWSEndpoint
;

ApiKeyLoc: 'header' | 'query';

AuthBlock:
    'type:' kind='basic'
    'username:' username=STRING
    'password:' password=STRING
  |  'type:' kind='bearer'
    'token:' token=STRING
  |  'type:' kind='api_key'
    'key:' key=STRING
    'in:' location=ApiKeyLoc
    'value:' value=STRING
;
// ---------- EXTERNAL (integration) ----------
ExternalRESTEndpoint:
  'ExternalREST' name=ID
  (
    'url:' url=STRING
    'verb:' verb=HttpVerb
    ('headers:' '[' headers+=Header (',' headers+=Header)* ']')?
    ('auth:' auth=AuthBlock)?  
    ('entity:' entity=[Entity])?
  )
  'end'
;

ExternalWSEndpoint:
  'ExternalWS' name=ID
  (
    'url:' url=STRING
    ('protocol:' protocol=STRING)?
    ('subprotocols:' subprotocols=StringList)?
    ('headers:' '[' headers+=Header (',' headers+=Header)* ']')?
    ('auth:' auth=AuthBlock)?  
    ('entity_in:' entity_in=[Entity])?
    ('entity_out:' entity_out=[Entity])?
    'mode:' mode=WSmode
  )#
  'end'
;

// ---------- INTERNAL (presentation) ----------
InternalRESTEndpoint:
  'InternalREST' name=ID
  (
    'entity:' entity=[Entity]
    'verb:' verb=HttpVerb
    ('path:' path=STRING)?
    ('summary:' summary=STRING)?
  )#
  'end'
;

WSmode:
    'publish' | 'subscribe' | 'duplex'
;

InternalWSEndpoint:
  'InternalWS' name=ID
  (
    ('entity_in:' entity_in=[Entity])?
    ('entity_out:' entity_out=[Entity])?
    'mode:' mode=WSmode
    ('path:' path=STRING)?
    ('summary:' summary=STRING)?
  )#
  'end'
;
// ---------- COMMON ----------
HttpVerb: 'GET' | 'POST' | 'PUT' | 'DELETE' | 'PATCH' ;

StringList: '[' (items+=STRING (',' items+=STRING)*)? ']' ;
Header: key=/[A-Za-z][A-Za-z0-9\-]*/ ':' value=STRING ;
