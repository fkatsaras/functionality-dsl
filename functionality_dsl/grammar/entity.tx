import utils
import expression

AttrType:
  'string' | 'integer' | 'number' | 'boolean' | 'array' | 'object'
;

// Format qualifiers for types (OpenAPI-aligned)
// Note: For datetime values, use plain 'string' type (OpenAPI date-time is just a string format hint)
TypeFormat:
  // String formats
  'email' | 'uri' | 'uuid_str' | 'date' | 'time' | 'hostname' | 'ipv4' | 'ipv6' |
  'byte' | 'binary' | 'password' | 'regex' |
  // Number formats
  'float' | 'double' | 'int32' | 'int64'
;

// Type constraint with range syntax: type(min..max) or type(exact)
TypeSpec:
  baseType=AttrType ('<' format=TypeFormat '>')? constraint=TypeConstraint? nullable='?'?
;

TypeConstraint:
  '(' range=RangeExpr ')'
;

RangeExpr:
    min=Number '..' max=Number    // Range: (1..10)
  | min=Number '..'                // Min only: (1..)
  | '..' max=Number                // Max only: (..10)
  | exact=Number                   // Exact: (5)
;

Number: FLOAT | INT;

AttrValue:
  STRING | FLOAT | INT | Bool
;

Attribute:
    name=ID ':' type=TypeSpec ('=' expr=Expr)? ';'
;

Entity:
  'Entity' name=ID
  ('(' parents+=[Entity] (',' parents+=[Entity])* ')')?
  (
      'source:' source=[Source]
    | 'attributes:' '-' attributes+=Attribute ('-' attributes+=Attribute)*
  )*
  'end'
;

// Entity sources / targets -------------------
Source:
    APIEndpointREST
  | APIEndpointWS
  | SourceREST
  | SourceWS
;

ApiKeyLoc: 'header' | 'query';

AuthBlock:
    'type:' kind='basic'
    'username:' username=STRING
    'password:' password=STRING
  |  'type:' kind='bearer'
    'token:' token=STRING
  |  'type:' kind='api_key'
    'key:' key=STRING
    'in:' location=ApiKeyLoc
    'value:' value=STRING
;

// ---------- SOURCE (integration) ----------
SourceREST:
  'Source<' kind='REST' '>' name=ID
  (
    'url:' url=STRING
    'verb:' verb=HttpVerb
    ('entity:' entity=[Entity])?
    ('headers:' '[' headers+=Header (',' headers+=Header)* ']')?
    ('auth:' auth=AuthBlock)?  
  )
  'end'
;

SourceWS:
  'Source<' kind='WS' '>' name=ID
  (
    'url:' url=STRING
    ('entity_in:' entity_in=[Entity])?
    ('entity_out:' entity_out=[Entity])?
    ('protocol:' protocol=STRING)?
    ('subprotocols:' subprotocols=StringList)?
    ('headers:' '[' headers+=Header (',' headers+=Header)* ']')?
    ('auth:' auth=AuthBlock)?  
  )#
  'end'
;

// ---------- API (presentation) ----------

// Parameter definition for path/query params
ParamDef:
  '-' name=ID ':' type=TypeSpec
;

PathParamsBlock:
  'path:' params+=ParamDef*
;

QueryParamsBlock:
  'query:' params+=ParamDef*
;

ParametersBlock:
  'parameters:'
    (path_params=PathParamsBlock)?
    (query_params=QueryParamsBlock)?
;

// Content type can be specified, defaults to application/json
ContentType:
  'application/json' | 'text/plain' | 'multipart/form-data' | 'application/xml'
;

// Schema reference: either inline type or Entity reference
// Try inline types first (primitive types), then entity references
SchemaRef:
    inline_type=InlineTypeSpec                // Inline type (integer, string, array<Entity>, etc.)
  | entity=[Entity]                           // Reference to Entity (must be uppercase first letter by convention)
;

// Inline type specification for request/response schemas
// Supports: integer, string, array<Product>, array<string>, etc.
InlineTypeSpec:
    'array' '<' itemType=[Entity] '>' constraint=TypeConstraint? nullable='?'?     // array<Product>
  | 'array' '<' primitive=AttrType '>' constraint=TypeConstraint? nullable='?'?     // array<string>
  | 'array' constraint=TypeConstraint? nullable='?'?                                // array (untyped)
  | baseType=AttrType ('<' format=TypeFormat '>')? constraint=TypeConstraint? nullable='?'?  // string, integer, string<email>, etc.
;

RequestBlock:
  'request:'
    (content_type=ContentType ':')?
      'schema:' schema=SchemaRef
;

ResponseBlock:
  'response:'
    (content_type=ContentType ':')?
      'schema:' schema=SchemaRef
;

APIEndpointREST:
  'APIEndpoint<' kind='REST' '>' name=ID
  (
    'path:' path=STRING
    'verb:' verb=HttpVerb
    (parameters=ParametersBlock)?
    (request=RequestBlock)?
    (response=ResponseBlock)?
    ('summary:' summary=STRING)?
    ('auth:' auth=AuthBlock)?
  )
  'end'
;

APIEndpointWS:
  'APIEndpoint<' kind='WS' '>' name=ID
  (
    'path:' path=STRING
    ('entity_in:' entity_in=[Entity])?
    ('entity_out:' entity_out=[Entity])?
    ('auth:' auth=AuthBlock)? 
    ('summary:' summary=STRING)? 
  )
  'end'
;

// ---------- COMMON ----------
HttpVerb: 'GET' | 'POST' | 'PUT' | 'DELETE' | 'PATCH' ;

StringList: '[' (items+=STRING (',' items+=STRING)*)? ']' ;
Header: key=/[A-Za-z][A-Za-z0-9\-]*/ ':' value=STRING ;
