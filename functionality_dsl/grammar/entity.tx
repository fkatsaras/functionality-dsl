import utils
import expression

AttrType:
  'string' | 'integer' | 'number' | 'boolean' | 'array' | 'object' | 'binary'
;

// Format qualifiers for types (OpenAPI-aligned)
// Note: For datetime values, use plain 'string' type (OpenAPI date-time is just a string format hint)
TypeFormat:
  // String formats
  'email' | 'uri' | 'uuid_str' | 'date' | 'time' | 'hostname' | 'ipv4' | 'ipv6' |
  'byte' | 'binary' | 'password' | 'regex' |
  // Number formats
  'float' | 'double' | 'int32' | 'int64'
;

// Type constraint with range syntax: type(min..max) or type(exact)
// Extended to support entity references: array<Entity>, object<Entity>
// Field markers:
//   @readonly - field excluded from create/update request schemas (response only)
//   @optional - field not required in create/update request schemas (can be omitted)
//   ? - nullable (value can be null)
TypeSpec:
    'array' '<' itemEntity=[Entity] '>' constraint=TypeConstraint? nullable='?'? (readonlyMarker='@readonly' | optionalMarker='@optional')?           // array<RealObject>
  | 'object' '<' nestedEntity=[Entity] '>' constraint=TypeConstraint? nullable='?'? (readonlyMarker='@readonly' | optionalMarker='@optional')?        // object<ObjectData>
  | baseType=AttrType ('<' format=TypeFormat '>')? constraint=TypeConstraint? nullable='?'? (readonlyMarker='@readonly' | optionalMarker='@optional')?
;

TypeConstraint:
  '(' range=RangeExpr ')'
;

RangeExpr:
    min=Number '..' max=Number    // Range: (1..10)
  | min=Number '..'                // Min only: (1..)
  | '..' max=Number                // Max only: (..10)
  | exact=Number                   // Exact: (5)
;

Number: FLOAT | INT;

AttrValue:
  STRING | FLOAT | INT | Bool
;

Attribute:
    name=ID ':' type=TypeSpec ('=' expr=Expr)? ';'
;

Entity:
  'Entity' name=ID
  ('(' parents+=ParentRef (',' parents+=ParentRef)* ')')?
  ('type:' ws_flow_type=WSFlowType)?
  ('source:' source=[Source])?
  ('strict:' strict=Bool)?
  (
    'attributes:' '-' attributes+=Attribute ('-' attributes+=Attribute)*
  )?
  (access=AccessBlock)?
  'end'
;

// Parent reference: either plain (Product) or aliased (thessForecast: Forecast)
ParentRef:
  (alias=ID ':')? entity=[Entity]
;

// WebSocket flow direction (for WebSocket entities only)
WSFlowType:
  'inbound' | 'outbound'
;


// Operation types (used in access control and source definitions)
Operation:
  'read' | 'create' | 'update' | 'delete' | 'subscribe' | 'publish'
;

// DEPRECATED: Source operations block with inline permissions (OLD SYNTAX)
// Keep for backward compatibility during migration
SourceOperationsBlock:
  'operations:'
    ('-' ops+=SourceOperationRule)+
;

SourceOperationRule:
  operation=Operation ':' '[' roles+=RoleOrWildcard (',' roles+=RoleOrWildcard)* ']'
;

RoleOrWildcard:
  STRING | '*'
;

// NEW SYNTAX: Simple operation list (permissions defined in AccessControl block)
SourceOperationsList:
  'operations:' '[' operations+=Operation (',' operations+=Operation)* ']'
;

// Access block for entities - controls authorization
// Supports three syntaxes:
// 1. access: public (public access to all operations)
// 2. access: [role1, role2] (all operations require these roles)
// 3. access: read: public create: [admin] (per-operation control)
AccessBlock:
    'access:' public_keyword='public'                             // Public access: access: public
  | 'access:' '[' roles+=ID (',' roles+=ID)* ']'                 // Role list: access: [admin, user]
  | 'access:' access_rules+=AccessRule+                           // Per-operation: access: read: public ...
;

// Access rule for per-operation control
// Example: read: public  or  create: [admin, librarian]
AccessRule:
  operation=Operation ':' (public_keyword='public' | '[' roles+=ID (',' roles+=ID)* ']')
;

// Entity sources / targets -------------------
Source:
    SourceREST
  | SourceWS
;

// ---------- SOURCE (integration) ----------
SourceREST:
  'Source<' kind='REST' '>' name=ID
  'url:' url=STRING
  (operations_list=SourceOperationsList | operations=SourceOperationsBlock)
  'end'
;

SourceWS:
  'Source<' kind='WS' '>' name=ID
  'channel:' url=STRING
  (operations_list=SourceOperationsList | operations=SourceOperationsBlock)?
  'end'
;

// ---------- COMMON ----------

// Content type can be specified for entities, defaults to application/json
ContentType:
    'application/json'
  | 'text/plain'
  | 'multipart/form-data'
  | 'application/x-www-form-urlencoded'
  | 'application/xml'
  | 'text/html'
  | 'application/pdf'
  | 'application/zip'
  | 'image/png'
  | 'image/jpeg'
  | 'application/octet-stream'
  | 'audio/wav'
  | 'audio/mpeg'
  | 'video/mp4'
;

HttpVerb: 'GET' | 'POST' | 'PUT' | 'DELETE' | 'PATCH' ;

StringList: '[' (items+=STRING (',' items+=STRING)*)? ']' ;
