import utils
import expression
import rbac
import auth

AttrType:
  'string' | 'integer' | 'number' | 'boolean' | 'array' | 'object' | 'binary'
;

// Format qualifiers for types (OpenAPI-aligned)
// Note: For datetime values, use plain 'string' type (OpenAPI date-time is just a string format hint)
TypeFormat:
  // String formats (datetime must come before date for correct PEG parsing)
  'email' | 'uri' | 'uuid' | 'datetime' | 'date' | 'time' | 'hostname' | 'ipv4' | 'ipv6' |
  'byte' | 'binary' | 'password' | 'regex' |
  // Number formats
  'float' | 'double' | 'int32' | 'int64'
;

// Type constraint with range syntax: type(min..max) or type(exact)
// Extended to support entity references: array<Entity>, object<Entity>
// Field markers:
//   @readonly - field excluded from create/update request schemas (response only)
//   @optional - field not required in create/update request schemas (can be omitted)
//   ? - nullable (value can be null)
TypeSpec:
    'array' '<' itemEntity=[Entity] '>' constraint=TypeConstraint? nullable='?'? (readonlyMarker='@readonly' | optionalMarker='@optional')?           // array<RealObject>
  | 'object' '<' nestedEntity=[Entity] '>' constraint=TypeConstraint? nullable='?'? (readonlyMarker='@readonly' | optionalMarker='@optional')?        // object<ObjectData>
  | baseType=AttrType ('<' format=TypeFormat '>')? constraint=TypeConstraint? nullable='?'? (readonlyMarker='@readonly' | optionalMarker='@optional')?
;

TypeConstraint:
  '(' range=RangeExpr ')'
;

RangeExpr:
    min=Number '..' max=Number    // Range: (1..10)
  | min=Number '..'                // Min only: (1..)
  | '..' max=Number                // Max only: (..10)
  | exact=Number                   // Exact: (5)
;

Number: FLOAT | INT;

AttrValue:
  STRING | FLOAT | INT | Bool
;

Attribute:
    name=ID ':' type=TypeSpec ('=' expr=Expr)? ';'
;

Entity:
  'Entity' name=ID
  ('(' parents+=ParentRef (',' parents+=ParentRef)* ')')?
  ('type:' ws_flow_type=WSFlowType)?
  ('source:' source=[Source])?
  ('strict:' strict=Bool)?
  (
    'attributes:' '-' attributes+=Attribute ('-' attributes+=Attribute)*
  )?
  (access=AccessBlock)?
  'end'
;

// Parent reference: either plain (Product) or aliased (thessForecast: Forecast)
ParentRef:
  (alias=ID ':')? entity=[Entity]
;

// WebSocket flow direction (for WebSocket entities only)
WSFlowType:
  'inbound' | 'outbound'
;


// Simple operation list (permissions defined in Entity AccessBlock)
// Note: Operation type is imported from rbac.tx
SourceOperationsList:
  'operations:' '[' operations+=Operation (',' operations+=Operation)* ']'
;

// Access block for entities - controls authorization
// Supports four syntaxes:
// 1. access: public (no auth required)
// 2. access: AuthName (valid auth of that type, no role check)
// 3. access: [role1, role2] (must have one of these roles - auth inferred from role)
// 4. access: read: public create: [admin] (per-operation control)
//
// In lists, you can mix Auth refs and Roles: [AuthName, role1, role2]
// - AuthName = any valid auth of that type
// - role = must have that role (via its associated auth)
AccessBlock:
    'access:' public_keyword='public'                             // Public access: access: public
  | 'access:' '[' access_items+=AccessItem (',' access_items+=AccessItem)* ']'  // List: access: [admin, APIKeyAuth]
  | 'access:' access_rules+=AccessRule+                           // Per-operation: access: read: public ...
  | 'access:' auth_ref=[Auth]                                     // Auth only: access: APIKeyAuth (last to avoid greedy ID match)
;

// Access item can be either a Role or an Auth reference
AccessItem:
    role=[Role] | auth=[Auth]
;

// Access rule for per-operation control
// Example: read: public  or  read: AuthName  or  create: [admin, librarian]
AccessRule:
    operation=Operation ':' (
        public_keyword='public'
      | '[' access_items+=AccessItem (',' access_items+=AccessItem)* ']'
      | auth_ref=[Auth]
    )
;

// Entity sources / targets -------------------
Source:
    SourceREST
  | SourceWS
;

// ---------- SOURCE (integration) ----------
SourceREST:
  'Source<' kind='REST' '>' name=ID
  'url:' url=STRING
  params=SourceParamsList?
  operations=SourceOperationsList
  ('auth:' auth=[Auth])?
  'end'
;

SourceWS:
  'Source<' kind='WS' '>' name=ID
  'channel:' url=STRING
  params=SourceParamsList?
  operations=SourceOperationsList?
  ('auth:' auth=[Auth])?
  'end'
;

// Source parameters list - maps client query params to source URL
// Params in URL path like {id} are replaced with query param values
// Params not in URL path are forwarded as query string
SourceParamsList:
  'params:' '[' params+=ID (',' params+=ID)* ']'
;

// ---------- COMMON ----------

// Content type can be specified for entities, defaults to application/json
// ContentType:
//     'application/json'
//   | 'text/plain'
//   | 'multipart/form-data'
//   | 'application/x-www-form-urlencoded'
//   | 'application/xml'
//   | 'text/html'
//   | 'application/pdf'
//   | 'application/zip'
//   | 'image/png'
//   | 'image/jpeg'
//   | 'application/octet-stream'
//   | 'audio/wav'
//   | 'audio/mpeg'
//   | 'video/mp4'
// ;

// HttpVerb: 'GET' | 'POST' | 'PUT' | 'DELETE' | 'PATCH' ;

StringList: '[' (items+=STRING (',' items+=STRING)*)? ']' ;
